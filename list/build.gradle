plugins {
    id 'com.android.dynamic-feature'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'de.mannodermaus.android-junit5'
}
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets.each { variant ->
        def variantName = variant.name

        variant.java.srcDir("src/$variantName/kotlin")

        if (variantName.toLowerCase().contains("test")) {
            variant.java.srcDirs += "src/testCommon/kotlin"
            variant.resources.srcDirs += "src/testCommon/resources"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }

        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
    namespace 'com.rey.list'
}

dependencies {
    ext {
        spek = '2.0.19'
    }

    implementation project(":app")
    kapt 'com.google.dagger:dagger-compiler:2.46.1'

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'
    testImplementation 'com.google.truth:truth:1.1.4'
    testImplementation 'com.squareup.okhttp3:mockwebserver:5.0.0-alpha.11'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}